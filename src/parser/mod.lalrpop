use ast::*;
use syntax::Token;

#[LALR]
#[recursive_ascent] grammar<'input>(text: &'input str);

pub Block: Box<Expr> = {
    <v:(<Expr> Semicolon)+> <e:Expr?> => Box::new(Expr::Block(v, match e {
        None => Box::new(Expr::Type(Type::Null)),
        Some(e) => e,
    })),
    Expr,
};

Expr: Box<Expr> = {
    ExprAss,
};

ExprAss: Box<Expr> = {
    ExprPlusMinus,
    <v:Id> <o:AssOp> <e:ExprPlusMinus> => Box::new(Expr::BinOp(Box::new(Expr::Type(Type::Id(v))), o, e)),
};

BinOpTier<Op, NextTier>: Box<Expr> = {
    NextTier,
    BinOpTier<Op, NextTier> Op NextTier => Box::new(Expr::BinOp(<>)),
};

ExprPlusMinus = BinOpTier<SumOp, ExprMulDiv>;
ExprMulDiv = BinOpTier<ProdOp, ExprPrefix>;

ExprPrefix: Box<Expr> = {
    ExprAtom,
    UnOp ExprAtom => Box::new(Expr::UnOp(<>)),
};

ExprAtom: Box<Expr> = {
    Lit,
    ExprControl,
    ExprSpread,
    Args => Box::new(Expr::Args(<>)),
    Null => Box::new(Expr::Type(Type::Null)),
    paren,
    scope,
};

paren: Box<Expr> = {
   Lparen <Block> Rparen,
};

scope: Box<Expr> = {
    Lbrace <Block> Rbrace => Box::new(Expr::Scope(<>)),
};

Lit: Box<Expr> = {
    Id => Box::new(Expr::Type(Type::Id(<>))),
    Integer => Box::new(Expr::Type(Type::Number(<>))),
    Float => Box::new(Expr::Type(Type::Float(<>))),
    Bool => Box::new(Expr::Type(Type::Bool(<>))),
    String => Box::new(Expr::Type(Type::String(<>))),
    Array => Box::new(Expr::Type(Type::Array(<>))),
};

Id: String = {
    TokId => <>.to_owned(),
};

Integer: i64 = {
    TokInteger => <>.parse::<i64>().unwrap(),
};

Float: f64 = {
    TokFloat => <>.parse::<f64>().unwrap(),
};

Bool: bool = {
    TokBool => <> == "true",
};

String: String = {
    TokString => <>.to_owned(),
};

ExprControl: Box<Expr> = {
    ExprIf,
    ExprFor,
};

ExprIf: Box<Expr> = {
    If <paren> <scope> => Box::new(Expr::If(<>, Box::new(Expr::Scope(Box::new(Expr::Type(Type::Null)))))),
    If <paren> <scope> Else <scopeOrIf> => Box::new(Expr::If(<>)),
};

scopeOrIf: Box<Expr> = {
    scope,
    ExprIf,
};

ExprFor: Box<Expr> = {
    For <ExprForArgs> <scope> => Box::new(Expr::For(<>)),
    For Lbrack Rbrack <ExprForArgs> <scope> => Box::new(Expr::ForA(<>)),
};

ExprForArgs: Box<Expr> = {
    Lparen <i:Id?> Comma <j:Id?> Comma <f:Expr> Range <t:Expr>  <s:(Colon <Expr>)?> Rparen => Box::new(
        Expr::Args(vec!(
            Box::new(Expr::Type(match i {
                None => Type::Null,
                Some(i) => Type::Id(i),
            })),
            Box::new(Expr::Type(match j {
                None => Type::Null,
                Some(j) => Type::Id(j),
            })),
            Box::new(Expr::Range(f, t, match s {
                None => Box::new(Expr::Type(Type::Number(1))),
                Some(e) => e,
            }))
        ))
    ),
    Lparen <j:Id?> Comma <f:Expr> Range <t:Expr>  <s:(Colon <Expr>)?> Rparen => Box::new(
        Expr::Args(vec!(
            Box::new(Expr::Type(match j {
                None => Type::Null,
                Some(j) => Type::Id(j),
            })),
            Box::new(Expr::Range(f, t, match s {
                None => Box::new(Expr::Type(Type::Number(1))),
                Some(e) => e,
            }))
        ))
    ),
    Lparen <f:Expr> Range <t:Expr>  <s:(Colon <Expr>)?> Rparen => Box::new(
        Expr::Args(vec!(
            Box::new(Expr::Range(f, t, match s {
                None => Box::new(Expr::Type(Type::Number(1))),
                Some(e) => e,
            }))
        ))
    ),
};

ExprSpread: Box<Expr> = {
};

Args = {
    Lparen <Separated<Comma>> Rparen,
};

Array = {
    Lbrack <Separated<Comma>> Rbrack,
};

Separated<S>: Vec<Box<Expr>> = {
    <v:(<Expr> S)+> <e:Expr?> => match e {
        None => v,
        Some(e) => {
            let mut v = v;
            v.push(e);
            v
        },
    },
};

UnOp: UnOpCode = {
    Minus => UnOpCode::Neg,
    Not => UnOpCode::Not,
};

EqOp: BinOpCode = {
    Equiv       => BinOpCode::Equ,
    NotEquiv    => BinOpCode::Neq,
    Less        => BinOpCode::Lt,
    LessEqual   => BinOpCode::LEt,
    Greater     => BinOpCode::Gt,
    GreatEqual  => BinOpCode::GEt,
};

AssOp: BinOpCode = {
    Equal => BinOpCode::Ass,
};

SumOp: BinOpCode = {
    Plus => BinOpCode::Add,
    Minus => BinOpCode::Sub,
};

ProdOp: BinOpCode = {
    Mult => BinOpCode::Mul,
    Divide => BinOpCode::Div,
};

extern {
    enum Token<'input> {
        Null        => Token::Null,
        For         => Token::KeyFor,
        If          => Token::KeyIf,
        Else        => Token::KeyElse,

        Lparen      =>  Token::OpLparen,
        Rparen      =>  Token::OpRparen,
        Lbrace      =>  Token::OpLbrace,
        Rbrace      =>  Token::OpRbrace,
        Lbrack      =>  Token::OpLbrack,
        Rbrack      =>  Token::OpRbrack,
        Minus       =>  Token::OpMinus,
        Plus        =>  Token::OpPlus,
        Divide      =>  Token::OpDivide,
        Mult        =>  Token::OpMult,
        Mod         =>  Token::OpMod,
        Power       =>  Token::OpPower,
        Land        =>  Token::OpLand,
        Lor         =>  Token::OpLor,
        Equal       =>  Token::OpEqual,
        Equiv       =>  Token::OpEquiv,
        NotEquiv    =>  Token::OpNotEquiv,
        Greater     =>  Token::OpGreater,
        Less        =>  Token::OpLess,
        GreatEqual  =>  Token::OpGreatEqual,
        LessEqual   =>  Token::OpLessEqual,
        And         =>  Token::OpAnd,
        Or          =>  Token::OpOr,
        Not         =>  Token::OpNot,
        Comma       =>  Token::OpComma,
        Colon       =>  Token::OpColon,
        Semicolon   =>  Token::OpSemicolon,
        Dot         =>  Token::OpDot,
        Range       =>  Token::OpRange,

        TokInteger     => Token::Integer(<&'input str>),
        TokFloat       => Token::Float(<&'input str>),
        TokId          => Token::Id(<&'input str>),
        TokString      => Token::String(<&'input str>),
        TokBool        => Token::Bool(<&'input str>),
    }
}
