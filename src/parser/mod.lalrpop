use ast::*;
use syntax::Token;
use lexer;
use interpreter::Env;

#[LALR]
#[recursive_ascent] grammar<'input>;

pub Block: Box<Expr> = {
    <v:(<Expr> Semicolon)+> <e:Expr?> => Box::new(Expr::Block(v, match e {
        None => Box::new(Expr::Type(Type::Null)),
        Some(e) => e,
    })),
    Expr,
};

Expr: Box<Expr> = {
    ExprAss,
};

ExprAss: Box<Expr> = {
    ExprFirstTier,
    Id AssOp ExprAss => Box::new(Expr::BinOp(<>)),
    Index AssOp ExprAss => Box::new(Expr::BinOp(<>)),
};

ExprFirstTier = ExprAndOr;

BinOpTier<Op, NextTier>: Box<Expr> = {
    NextTier,
    BinOpTier<Op, NextTier> Op NextTier => Box::new(Expr::BinOp(<>)),
};

ExprAndOr = BinOpTier<CompOp, ExprEquality>;
ExprEquality = BinOpTier<EqOp, ExprPlusMinus>;
ExprPlusMinus = BinOpTier<SumOp, ExprMulDiv>;
ExprMulDiv = BinOpTier<ProdOp, ExprPower>;
ExprPower = BinOpTier<PowOp, ExprPrefix>;

ExprPrefix: Box<Expr> = {
    ExprAtom,
    UnOp ExprAtom => Box::new(Expr::UnOp(<>)),
};

ExprAtom: Box<Expr> = {
    Lit,
    Index,
    ExprControl,
    ExprSpread,
    ExprNatFun,
    ImportExpr,
    Null => Box::new(Expr::Type(Type::Null)),
    ExprBreakReturn,
    paren,
    scope,
};

ImportExpr: Box<Expr> = {
    Import <String> => Box::new(Expr::Import(<>)),
};

paren: Box<Expr> = {
   Lparen <Block> Rparen,
};

scope: Box<Expr> = {
    Lbrace <Block> Rbrace => Box::new(Expr::Scope(<>)),
};

Lit: Box<Expr> = {
    Id,
    Integer => Box::new(Expr::Type(Type::Number(<>))),
    Float => Box::new(Expr::Type(Type::Float(<>))),
    Bool => Box::new(Expr::Type(Type::Bool(<>))),
    String => Box::new(Expr::Type(Type::String(<>))),
    Array => Box::new(Expr::Type(Type::Array(<>))),
    ObjectDef => Box::new(Expr::ObjectDef(<>)),
    ExprFunction,
};

Index: Box<Expr> = {
    <i:Id> Lbrack <e:Expr> Rbrack => Box::new(Expr::Index(i,e)),
    <i:Index> Lbrack <e:Expr> Rbrack => Box::new(Expr::Index(i,e)),
    <a:Array> Lbrack <e:Expr> Rbrack => Box::new(Expr::Index(Box::new(Expr::Type(Type::Array(a))), e)),
    <ForA> Lbrack <Expr> Rbrack => Box::new(Expr::Index(<>)),
    <WhileA> Lbrack <Expr> Rbrack => Box::new(Expr::Index(<>)),
    <paren> Lbrack <Expr> Rbrack => Box::new(Expr::Index(<>)),
    <scope> Lbrack <Expr> Rbrack => Box::new(Expr::Index(<>)),
    <i:Id> Dot <ind:TokId> => Box::new(Expr::Index(i,Box::new(Expr::Type(Type::String(ind.to_owned()))))),
    <i:Index> Dot <ind:TokId> => Box::new(Expr::Index(i,Box::new(Expr::Type(Type::String(ind.to_owned()))))),
};

Id: Box<Expr> = {
    TokId => Box::new(Expr::Type(Type::Id(<>.to_owned()))),
};

Integer: i64 = {
    TokInteger => <>.parse::<i64>().unwrap(),
};

Float: f64 = {
    TokFloat => <>.parse::<f64>().unwrap(),
};

Bool: bool = {
    TokBool => <> == "true",
};

String: String = {
    TokString => <>.to_owned(),
};

ExprNatFun: Box<Expr> = {
    NatFun paren => Box::new(Expr::NatFun(<>)),
};

ExprFunction: Box<Expr> = {
    Function <a:Args> <s:scope> => Box::new(Expr::Type(Type::Function(a,s,Env::new_root()))),
};

ExprBreakReturn: Box<Expr> = {
    Break => Box::new(Expr::Type(Type::Break(Box::new(Expr::Type(Type::Null))))),
    Break <Lit> => Box::new(Expr::Type(Type::Break(<>))),
    Break <paren> => Box::new(Expr::Type(Type::Break(<>))),
    Return => Box::new(Expr::Type(Type::Return(Box::new(Expr::Type(Type::Null))))),
    Return <Lit> => Box::new(Expr::Type(Type::Return(<>))),
    Return <paren> => Box::new(Expr::Type(Type::Return(<>))),
};

ExprControl: Box<Expr> = {
    ExprIf,
    ExprWhile,
    ExprFor,
    ExprFuncCall,
};

ExprIf: Box<Expr> = {
    If <Expr> <scope> => Box::new(Expr::If(<>, Box::new(Expr::Scope(Box::new(Expr::Type(Type::Null)))))),
    If <Expr> <scope> Else <scopeOrIf> => Box::new(Expr::If(<>)),
};

scopeOrIf: Box<Expr> = {
    scope,
    ExprIf,
};

ExprWhile: Box<Expr> = {
    While <Expr> <scope> => Box::new(Expr::While(<>)),
    WhileA,
};

WhileA: Box<Expr> = {
    WhileArr <Expr> <scope> => Box::new(Expr::WhileA(<>)),
};

ExprFor: Box<Expr> = {
    For <ExprForArgs> <scope> => Box::new(Expr::For(<>)),
    ForA,
};

ForA: Box<Expr> = {
    ForArr <ExprForArgs> <scope> => Box::new(Expr::ForA(<>)),
};

ExprForArgs: Box<Expr> = {
    Lparen <i:Id?> Comma <j:Id?> Comma <f:Expr> Range <t:Expr>  <s:(Colon <Expr>)?> Rparen => Box::new(
        Expr::Args(vec!(
            match i {
                None => Box::new(Expr::Type(Type::Null)),
                Some(i) => i,
            },
            match j {
                None => Box::new(Expr::Type(Type::Null)),
                Some(j) => j,
            },
            Box::new(Expr::Range(f, t, match s {
                None => Box::new(Expr::Type(Type::Number(1))),
                Some(e) => e,
            }))
        ))
    ),
    Lparen <j:Id?> Comma <f:Expr> Range <t:Expr>  <s:(Colon <Expr>)?> Rparen => Box::new(
        Expr::Args(vec!(
            match j {
                None => Box::new(Expr::Type(Type::Null)),
                Some(j) => j,
            },
            Box::new(Expr::Range(f, t, match s {
                None => Box::new(Expr::Type(Type::Number(1))),
                Some(e) => e,
            }))
        ))
    ),
    Lparen <f:Expr> Range <t:Expr>  <s:(Colon <Expr>)?> Rparen => Box::new(
        Expr::Args(vec!(
            Box::new(Expr::Range(f, t, match s {
                None => Box::new(Expr::Type(Type::Number(1))),
                Some(e) => e,
            }))
        ))
    ),
};

ExprSpread: Box<Expr> = {
};

ExprFuncCall: Box<Expr> = {
    <Id> <Args> => Box::new(Expr::FuncCall(<>)),
    <Index> <Args> => Box::new(Expr::FuncCall(<>)),
    <paren> <Args> => Box::new(Expr::FuncCall(<>)),
    <scope> <Args> => Box::new(Expr::FuncCall(<>)),
    <ExprFunction> <Args> => Box::new(Expr::FuncCall(<>)),
    <ExprControl> <Args> => Box::new(Expr::FuncCall(<>)),
};

Args: Box<Expr> = {
    Lparen <Separated<Expr, Comma>> Rparen => Box::new(Expr::Args(<>)),
    Lparen <Expr> Rparen => Box::new(Expr::Args(vec!(<>))),
    Lparen Rparen => Box::new(Expr::Args(vec!())),
};

Array = {
    Lbrack Rbrack => vec!(),
    Lbrack <Expr> Rbrack => vec!(<>),
    Lbrack <Separated<Expr, Comma>> Rbrack,
};

Separated<E, S>: Vec<Box<Expr>> = {
    <v:(<E> S)+> <e:E?> => match e {
        None => v,
        Some(e) => {
            let mut v = v;
            v.push(e);
            v
        },
    },
};

ObjectDef = {
    Dollar Lbrace Rbrace => vec!(),
    Dollar Lbrace <ObjectMember> Rbrace => vec!(<>),
    Dollar Lbrace <Separated<ObjectMember, Comma>> Rbrace,
};

ObjectMember: Box<Expr> = {
    <id:TokId> Colon <e:Expr> => Box::new(Expr::ObjectMember(Box::new(Expr::Type(Type::String(id.to_owned()))), e)),
    <id:String> Colon <e:Expr> => Box::new(Expr::ObjectMember(Box::new(Expr::Type(Type::String(id.to_owned()))), e)),
};

UnOp: UnOpCode = {
    Minus       => UnOpCode::Neg,
    Not         => UnOpCode::Not,
    Len         => UnOpCode::Len,
};

EqOp: BinOpCode = {
    Equiv       => BinOpCode::Equ,
    NotEquiv    => BinOpCode::Neq,
    Less        => BinOpCode::Lt,
    LessEqual   => BinOpCode::LEt,
    Greater     => BinOpCode::Gt,
    GreatEqual  => BinOpCode::GEt,
};

CompOp: BinOpCode = {
    And         => BinOpCode::And,
    Or          => BinOpCode::Or,
};

AssOp: BinOpCode = {
    Equal       => BinOpCode::Ass,
    MinusEq     => BinOpCode::SubEq,
    PlusEq      => BinOpCode::AddEq,
    MultEq      => BinOpCode::MulEq,
    DivideEq    => BinOpCode::DivEq,
    ModEq       => BinOpCode::ModEq,
};

SumOp: BinOpCode = {
    Plus        => BinOpCode::Add,
    Minus       => BinOpCode::Sub,
};

ProdOp: BinOpCode = {
    Mult        => BinOpCode::Mul,
    Divide      => BinOpCode::Div,
    Mod         => BinOpCode::Mod,
};

PowOp: BinOpCode = {
    Pow         => BinOpCode::Pow,
};

NatFun: NatFunction = {
    Floor       => NatFunction::Floor,
    Ceil        => NatFunction::Ceil,
    Rand        => NatFunction::Rand,
};

extern {
    type Location = usize;
    type Error = lexer::LexicalError<'input>;

    enum Token<'input> {
        Null        => Token::Null,
        For         => Token::KeyFor,
        ForArr      => Token::KeyForA,
        While       => Token::KeyWhile,
        WhileArr    => Token::KeyWhileA,
        If          => Token::KeyIf,
        Else        => Token::KeyElse,
        Break       => Token::KeyBreak,
        Return      => Token::KeyReturn,
        Function    => Token::KeyFunction,
        Floor       => Token::KeyFloor,
        Ceil        => Token::KeyCeil,
        Rand        => Token::KeyRand,
        Import      => Token::KeyImport,

        Lparen      =>  Token::OpLparen,
        Rparen      =>  Token::OpRparen,
        Lbrace      =>  Token::OpLbrace,
        Rbrace      =>  Token::OpRbrace,
        Lbrack      =>  Token::OpLbrack,
        Rbrack      =>  Token::OpRbrack,
        Minus       =>  Token::OpMinus,
        Plus        =>  Token::OpPlus,
        Divide      =>  Token::OpDivide,
        Mult        =>  Token::OpMult,
        MinusEq     =>  Token::OpMinusEq,
        PlusEq      =>  Token::OpPlusEq,
        DivideEq    =>  Token::OpDivideEq,
        MultEq      =>  Token::OpMultEq,
        Mod         =>  Token::OpMod,
        ModEq       =>  Token::OpModEq,
        Pow         =>  Token::OpPower,
        Land        =>  Token::OpLand,
        Lor         =>  Token::OpLor,
        Equal       =>  Token::OpEqual,
        Equiv       =>  Token::OpEquiv,
        NotEquiv    =>  Token::OpNotEquiv,
        Greater     =>  Token::OpGreater,
        Less        =>  Token::OpLess,
        GreatEqual  =>  Token::OpGreatEqual,
        LessEqual   =>  Token::OpLessEqual,
        And         =>  Token::OpAnd,
        Or          =>  Token::OpOr,
        Not         =>  Token::OpNot,
        Comma       =>  Token::OpComma,
        Colon       =>  Token::OpColon,
        Semicolon   =>  Token::OpSemicolon,
        Dot         =>  Token::OpDot,
        Range       =>  Token::OpRange,
        Len         =>  Token::OpLength,
        Dollar      =>  Token::OpDollar,

        TokInteger     => Token::Integer(<&'input str>),
        TokFloat       => Token::Float(<&'input str>),
        TokId          => Token::Id(<&'input str>),
        TokString      => Token::String(<&'input str>),
        TokBool        => Token::Bool(<&'input str>),
    }
}
